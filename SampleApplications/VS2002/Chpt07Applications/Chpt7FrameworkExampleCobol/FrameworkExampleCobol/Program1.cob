000010*
000020 CLASS-ID. MYFIRSTCLASS. 
000030 ENVIRONMENT DIVISION.
000040 CONFIGURATION SECTION.
000050 REPOSITORY.
000060*
000070*    Reference a .NET Framework Class 
000080*    These classes are from the 
000081*    System.Text.RegularExpressions Namespace
000090*
000100     CLASS Regex AS "System.Text.RegularExpressions.Regex"
000110     CLASS Match AS "System.Text.RegularExpressions.Match"
000120* 
000130*    Reference a .NET Framework Delegate
000140*    The Delegate is from the 
000141*    System.Text.RegularExpressions Namespace
000150*
000160     DELEGATE MatchEvaluator AS 
000170			  "System.Text.RegularExpressions.MatchEvaluator"
000180*     
000190*    Reference a .NET Framework Class
000200*    This Class is from the System Namespace
000210*
000220     CLASS SYS-STRING AS "System.String"
000230       
000240 STATIC.
000250 PROCEDURE DIVISION.
000260 METHOD-ID. MAIN.
000270 DATA DIVISION.
000280 WORKING-STORAGE SECTION.
000290 01 OBJ OBJECT REFERENCE MYFIRSTCLASS.
000300 PROCEDURE DIVISION.
000310     INVOKE MYFIRSTCLASS "NEW" RETURNING OBJ.
000320 END METHOD MAIN.
000330 END STATIC.
000340 
000350 OBJECT.
000360 DATA DIVISION.
000370 WORKING-STORAGE SECTION.
000380 PROCEDURE DIVISION.
000390*
000400 METHOD-ID. NEW.
000410 DATA DIVISION.
000420 WORKING-STORAGE SECTION.
000430 PROCEDURE DIVISION.
000440     INVOKE SELF "MYFIRSTMETHOD".
000450 END METHOD NEW.
000460* 
000470 METHOD-ID. MYFIRSTMETHOD.
000480 DATA DIVISION.
000490 WORKING-STORAGE SECTION.
000500*
000510* Establish appropriate References to the .NET Framework objects 
000520*
000530  01 MY-DELEGATE OBJECT REFERENCE MatchEvaluator.
000540  01 INPUT_STRING OBJECT REFERENCE SYS-STRING.
000550  01 OUTPUT_STRING OBJECT REFERENCE SYS-STRING.
000560  01 STRING_PATTERN OBJECT REFERENCE SYS-STRING.
000570  01 DISPLAY_LINE PIC X(100).
000580  01 NULL-X PIC X(1).
000590  
000600 LINKAGE SECTION.
000610 PROCEDURE DIVISION.
000620* 
000630*    Assigning the Delegate via the Class Constructor
000640*
000650	   INVOKE MatchEvaluator "NEW" 
000660			USING BY VALUE SELF "EvaluatorMethod"  
000670          RETURNING MY-DELEGATE.
000680*          
000690     SET INPUT_STRING TO "I think programming on the .NET platform is 0."
000700     SET STRING_PATTERN TO "\d"
000710*     
000720*    Using a .NET Framework Class and doing an INLINE Invoke 
000730*
000740     SET OUTPUT_STRING TO REGEX::"Replace" 
000750		   (INPUT_STRING, STRING_PATTERN, MY-DELEGATE)
000760     SET DISPLAY_LINE TO OUTPUT_STRING 
000770     
000780     DISPLAY DISPLAY_LINE
000790     DISPLAY "Enter X and Press Enter to Exit.".
000800     ACCEPT NULL-X.
000810     
000820 END METHOD MYFIRSTMETHOD.
000830*
000840*    Using the Public Shared Function that is 
000850*    fired each time the Regex.Replace method finds a match
000860*
000870 METHOD-ID. MatchEvaluatorMethod as "EvaluatorMethod".
000880 DATA DIVISION.
000890 WORKING-STORAGE SECTION.
000900 LINKAGE SECTION.
000910   01 MY-MATCH OBJECT REFERENCE Match.
000920   01 MyReturnString OBJECT REFERENCE SYS-STRING.
000930 PROCEDURE DIVISION USING BY VALUE MY-MATCH 
000940						RETURNING MyReturnString.
000950*
000960* Optionally, you could make the return value
000970* conditional depending on the MATCH value.
000980*
000990	    SET MyReturnString TO "Easy"
001000 END METHOD MatchEvaluatorMethod.
001010 
001020 END OBJECT.
001030 END CLASS MYFIRSTCLASS.